[[model]]
name: model-green
author: Ines Krissaane
desc: Check associated model definition document
# Initial values
ikr.C1 = 1
ikr.O = 0

#
# Simulation engine variables
#
[engine]
time = 0 bind time
pace = 0 bind pace

#
# Membrane potential
#
[membrane]
V = engine.pace
    desc: membrane potential
    in [mV]

[nernst]
EK = -85 [mV]

#
#
[ikr]
use membrane.V
IKr = p11 * O * (V - nernst.EK)
I = 1 - (O + C1)


k1 = p1 * exp(p2 * V)
k2 = p3 * exp(-p4 * V)
k3 = p5 * exp(p6 * V)
k4 = p7 * exp(-p8 * V)
k5 = p9 * exp(-p10 * V)
k6 = (k5*k2*k4)/(k1*k3)


dot(C1) = +k2*O  + k6*I - C1*(k1+ k5)
dot(O)  = +k4*I  + k1*C1 - O*(k2 + k3)

p1 = 2.26e-4 [1/ms]
p2 = 0.06990 [1/mV]
p3 = 3.45e-5 [1/ms]
p4 = 0.05462 [1/mV]
p5 = 0.08730 [1/ms]
p6 = 8.91e-3 [1/mV]
p7 = 5.15e-3 [1/ms]
p8 = 0.03158 [1/mV]
p9 = 5.15e-3 [1/ms]
p10 = 0.03158 [1/mV]
p11 = 0.15240 [mS/uF]

n_params = 11
